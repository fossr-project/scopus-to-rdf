PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX fossr: <http://fossr.eu/kg/onto/>

SELECT
?researcher (COUNT(DISTINCT ?affiliation) AS ?num_affiliations)
WHERE {
  	[] a schema:ScholarlyArticle;
    	fossr:hasAuthorAffiliation [
    		fossr:forAuthor ?researcher;
    		fossr:hasAffiliation ?affiliation
  		];
    	dcterms:date ?last_publication_date.
    {
        SELECT
            ?researcher
            (MAX(?date) AS ?last_publication_date)
        WHERE {
          	[] a schema:ScholarlyArticle;
    	        fossr:hasAuthorAffiliation/fossr:forAuthor ?researcher;
            	dcterms:date ?date.
        }
        GROUP BY ?researcher
    }
}
GROUP BY ?researcher
ORDER BY DESC(?num_affiliations)
